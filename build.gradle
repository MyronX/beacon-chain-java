plugins {
  id "io.spring.dependency-management" version "1.0.6.RELEASE"
  id 'com.github.kt3k.coveralls' version '2.8.2'
}

allprojects {
  apply plugin: 'java-library'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'jacoco'

  apply from: "${rootDir}/versions.gradle"

  version = rootProject.version

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  dependencies {
    implementation 'com.google.code.findbugs:jsr305'
    implementation 'org.javatuples:javatuples'

    testImplementation 'junit:junit'
    testImplementation 'org.assertj:assertj-core'
  }

  repositories {
    jcenter()
  }

  task allDependencies(type: DependencyReportTask) {}

  javadoc {
    options.author = true
    options.header = project.name
    options.addStringOption('-quiet')
    options.encoding = "UTF-8"
    options.links(
            "http://docs.oracle.com/javase/8/docs/api/"
    )
  }

  jacoco {
    toolVersion = '0.8.2'
  }

  jacocoTestReport {
    reports {
      xml.enabled = true
      html.enabled = true
    }
  }
}

// Aggregates all subprojects source paths so coveralls knows where the sources are
coveralls {
  def subProjects = subprojects.findAll()
  sourceDirs = subProjects.sourceSets.main.allSource.srcDirs.flatten()
  jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}

// aggregates jacoco results from all subprojects and core project and generate a report
task jacocoRootTestReport(type: JacocoReport) {

  sourceSets sourceSets.main

  def jacocoTestFiles = []
  subprojects.each { p ->
    def coverageFileLocation = "$p.buildDir/jacoco/test.exec"
    if (new File(coverageFileLocation).exists()) {
      jacocoTestFiles << coverageFileLocation
    }
  }

  logger.info('Aggregating next JaCoCo Coverage Files: {}', jacocoTestFiles)
  executionData files(jacocoTestFiles)

  reports {
    xml.enabled true
    csv.enabled false
    html.enabled true
    xml.setDestination(file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml"))
    html.setDestination(file("${buildDir}/reports/jacoco/test/jacocoTestReport"))
  }
}
